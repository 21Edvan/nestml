neuron iaf_psc_delta_nestml:

  state:
    V_1 mV = 0 mV
    refr_spikes_buffer mV = 0 mV
    alias V_m mV = V_1 + E_L # Membrane potential.
  end

  equations:
    shape G = delta(t, tau_m)
    V_1' = -1/tau_m * V_1 + 1/C_m * (I_sum(G, spikes) + I_e + currents)
  end

  parameter:
    h       ms = resolution()
    tau_m   ms = 10    # Membrane time constant.
    C_m     pF = 250     # Capacity of the membrane
    t_ref   ms = 2       # Refractory period.
    tau_syn ms = 2       # Time constant of synaptic current.
    E_L     mV = -70     # Resting potential.
    I_e     pA = 0       # External current.
    alias V_reset mV = -70 - E_L
    alias Theta   mV = -55 - E_L
    V_min mV = -inf
    with_refr_input boolean = false
  end

  internal:
    r  integer
    RefractoryCounts integer = steps(t_ref)
  end

  input:
    spikes   <- spike
    currents <- current
  end

  output: spike

  update:
    if r == 0: # not refractory
      integrate(V_1)
      # if we have accumulated spikes from refractory period,
      # add and reset accumulator
      if with_refr_input and refr_spikes_buffer != 0.0:
        V_1 += refr_spikes_buffer;
        refr_spikes_buffer = 0.0 mV;
      end
      # lower bound of membrane potential
      if V_1 < V_min:
        V_1 = V_min
      end
    else: # neuron is absolute refractory
      # read spikes from buffer and accumulate them, discounting
      # for decay until end of refractory period
      if with_refr_input:
        refr_spikes_buffer += spikes * exp(r * h / tau_m);
      else:
        spikes.getSum() # clear buffer entry, ignore spike
      end
      r -= 1
    end

    if V_1 >= Theta: # threshold crossing
        r = RefractoryCounts
        V_1 = V_reset
        emit_spike()
    end

  end

end
