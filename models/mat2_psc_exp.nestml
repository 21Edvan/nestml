neuron mat2_psc_exp_nestml:

  state:
    V_1 mV
    V_th_alpha_1 mV
    V_th_alpha_2 mV
    alias V_m mV = V_1 + E_L # Membrane potential.
  end

  equations:
    shape I_shape_in = exp(-1/tau_syn_in*t)
    shape I_shape_ex = exp(-1/tau_syn_ex*t)

    # V_th_alpha_1' = -V_th_alpha_1/tau_1
    # V_th_alpha_2' = -V_th_alpha_2/tau_2

    V_1' = -1/tau_m * V_1 + 1/C_m * (I_sum(I_shape_in, in_spikes) + I_sum(I_shape_ex, ex_spikes) + I_e + currents)
  end

  parameter:
    tau_m        ms =     5ms  # Membrane time constant in ms
    C_m          pF =   100pF  # Capacity of the membrane in pF
    t_ref        ms =     2ms  # Duration of absolute refractory period (no spiking) in ms
    E_L          mV = -70.0mV  # Resting potential in mV
    I_e          pA =     0pA  # Constant input current in pA
    tau_syn_ex   ms =     1ms  # Time constant of postsynaptic excitatory currents in ms
    tau_syn_in   ms =     3ms  # Time constant of postsynaptic inhibitory currents in ms
    tau_1        ms =    10ms  # Short time constant of adaptive threshold in ms
    tau_2        ms =   200ms  # Long time constant of adaptive threshold in ms
    alpha_1      mV =  37.0mV  # Amplitude of short time threshold adaption in mV [3]
    alpha_2      mV =   2.0mV  # Amplitude of long time threshold adaption in mV [3]
    omega        mV =   19.0mV # Resting spike threshold in mV (absolute value, not relative to E_L)
  end

  internal:
    r integer
    h_new ms = resolution()
    P11th real = exp( -h_new / tau_1 )
    P22th real = exp( -h_new / tau_2 )
    RefractoryCounts integer = steps(t_ref)
  end

  input:
    ex_spikes   <- excitatory spike
    in_spikes   <- inhibitory spike
    currents    <- current
  end

  output: spike

  update:
    integrate(V_1)
    #integrate(V_th_alpha_1)
    #integrate(V_th_alpha_2)

    V_th_alpha_1 *= P11th
    V_th_alpha_2 *= P22th

    if r == 0: # not refractory
      if V_1 >= omega + V_th_alpha_1 + V_th_alpha_2: # threshold crossing
          r = RefractoryCounts

          # procedure for adaptive potential
          V_th_alpha_1 += alpha_1 # short time
          V_th_alpha_2 += alpha_2 # long time

          emit_spike()
      end
    else:
        r = r - 1
    end

  end

end
