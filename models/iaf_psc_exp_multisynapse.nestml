neuron iaf_psc_exp_multisynapse_nestml:

  state:
    V_abs mV
    alias V_m mV = V_abs + E_L # Membrane potential.
  end

  equations:
      shape I_shape = exp(-1/tau_syn*t)
      V_abs' = -1/Tau * V_abs + 1/C_m * (I_sum(I_shape, spikes) + I_e + currents)
  end

  parameter:
    receptors integer  = 100
    C_m     pF = 250   # Capacity of the membrane
    Tau     ms = 10    # Membrane time constant.
    suppress tau_syn ms[receptors] = 2     # Time constant of synaptic current.
    t_ref   ms = 2     # Refractory period.
    E_L     mV = 0  # Resting potential.
    alias V_reset mV = -55.0 - E_L
    alias Theta   mV = -70.0 - E_L
    I_e     pA = 0     # External current.
  end

  function set_V_m(val mV):
    V_m = val
  end

  function set_V_reset(val mV):
    V_reset = val
  end

  function set_Theta(val mV):
    Theta = val
  end


  internal:
    RefractoryCounts integer = steps(t_ref) # refractory time in steps
    r integer                               # counts number of tick during the refractory period
  end

  input:
    spikes[receptors]   <- spike
    currents <- current
  end

  output: spike

  update:
    if r == 0: # not refractory
      integrate(V_abs)
    else:
      r = r - 1
    end

    if V_abs >= Theta: # threshold crossing
        r = RefractoryCounts
        V_abs = V_reset
        emit_spike()
    end

  end

end
