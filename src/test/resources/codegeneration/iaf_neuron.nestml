neuron iaf_neuron_nestml:

  state:
    V mV
    G real
    alias V_m mV = V + E_L # Membrane potential.
  end

  equations:
      G = (e/tau_syn) * t * exp(-1/tau_syn*t)
      V' = -1/Tau * V + 1/C_m * (I_sum(G, spikes) + I_e + currents)
  end

  parameter:
    C_m     pF = 250 [[C_m > 0]]   # Capacity of the membrane
    Tau     ms = 10    # Membrane time constant.
    tau_syn ms = 2     # Time constant of synaptic current.
    t_ref   ms = 2     # Refractory period.
    E_L     mV = -70  # Resting potential.
    alias V_reset mV = -70 - E_L
    alias Theta   mV = -55 - E_L
    I_e     pA = 0     # External current.
  end

  internal:
    h   ms   = resolution()
    r  integer
    RefractoryCounts integer = steps(t_ref)
  end

  input:
    spikes   <- spike
    currents <- current
  end

  output: spike

  update:
    if r == 0: # not refractory
        integrate(V)

    else:
        r = r - 1
    end
    if V >= Theta: # threshold crossing
        r = RefractoryCounts
        V = V_reset
        emitSpike()
    end


  end

end