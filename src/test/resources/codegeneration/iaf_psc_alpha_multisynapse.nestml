neuron iaf_psc_alpha_multisynapse_nestml:

  state:
    V mV
    suppress I_shape real[receptors] # suppress is, as there is errors in logging
    alias V_m mV = V + E_L # Membrane potential.
  end

  equations:
      I_shape = (e/tau_syn) * t * exp(-1/tau_syn*t)
      V' = -1/Tau * V + 1/C_m * (I_sum(I_shape, spikes) + I_e + currents)
  end

  parameter:
    receptors integer  = 100
    C_m     pF = 250   # Capacity of the membrane
    Tau     ms = 10    # Membrane time constant.
    suppress tau_syn ms[receptors] = 2     # Time constant of synaptic current.
    t_ref   ms = 2     # Refractory period.
    E_L     mV = -70   # Resting potential.
    alias V_reset mV = -70.0 - E_L
    alias Theta   mV = -55.0 - E_L
    I_e     pA = 0     # External current.
  end

  internal:
    r  integer
    RefractoryCounts integer = steps(t_ref)
  end

  input:
    spikes[receptors]   <- excitatory spike
    currents <- current
  end

  output: spike

  update:
    if r == 0: # not refractory
      integrate(V)
    else:
      r = r - 1
    end

    if V >= Theta: # threshold crossing
        r = RefractoryCounts
        V = V_reset
        emit_spike()
    end

  end

end