/*
  A straight forward implementation of the RC circuit approach.
  Adds alpha shapes for synaptic currents.
*/
neuron rc_alpha:

  state:
    V_abs mV = 0mV
    alias V_m mV = V_abs + E_L
    g_ex pA = 0pA
  end

  equations:
    g_ex'' = -g_ex' / tau_syn
    g_ex' = g_ex' - ( g_ex / tau_syn )
    I_syn pA = g_ex + currents + I_e
    V_abs' = -V_abs/tau_m  + I_syn/C_m
  end

  parameter:
    E_L   mV = -70mV
    C_m   pF = 250pF
    tau_m ms = 10ms
    tau_syn ms = 2.0ms

    alias V_th mV = -55mV - E_L
    alias V_reset mV = -70mV - E_L
    refractory_timeout ms = 2ms
    refractory_counts integer = 0
    I_e pA = 0pA
  end

  internal:
    PSConInit_E pA/ms = 1.0 pA * e / tau_syn
  end

  input:
    spikes   <- spike
    currents <- current
  end

  output: spike

  update:
    if refractory_counts == 0:
      integrate_odes()
      if V_abs > V_th:
        V_abs = V_reset
        emit_spike()
        refractory_counts = steps(refractory_timeout)
      end
    else:
      refractory_counts -= 1
    end

    g_ex' += PSConInit_E * spikes
  end

end
