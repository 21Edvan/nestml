neuron izhikevich_neuron:

  initial_values:
    v mV = -65mV # Membrane potential in mV
    u real = 0   # Membrane potential recovery variable
  end

  equations:
    v' = ( 0.04 * v * v / mV + 5.0 * v + ( 140 - u ) * mV + ( I * GOhm ) ) / ms
    u' = a*(b*v-u * mV) / (mV*ms)
  end

  parameters:
    a real = 0.02   # describes time scale of recovery variable
    b real = 0.2    # sensitivity of recovery variable
    c mV = -65 mV   # after-spike reset value of v
    d real = 8.0    # after-spike reset value of u
  end

  input:
    spikes mV <- spike
    I <- current
  end

  output: spike

  update:
    integrate_odes()
    # Add synaptic current
    v += spikes

    # threshold crossing
    if v >= 30mV:
      v = c
      u += d
      emit_spike()
    end

  end

end
